@title Synchronous threads
@tangler fibres.flx=share/lib/std/control/fibres.flx
@tangler schannels.flx=share/lib/std/control/schannels.flx
@tangler mux.flx=share/lib/std/control/mux.flx
@tangler spipes.flx=share/lib/std/control/spipes.flx

@tangler spipeexpr.fsyn = share/lib/std/control/spipeexpr.fsyn
@tangler spipesex-01.flx=test/regress/rt/spipesex-01.flx
@tangler spipesex-01.expect=test/regress/rt/spipesex-01.expect
@tangler spipesex-02.flx=test/regress/rt/spipesex-02.flx
@tangler spipesex-02.expect=test/regress/rt/spipesex-02.expect
@tangler spipesex-02a.flx=test/regress/rt/spipesex-02a.flx
@tangler spipesex-02a.expect=test/regress/rt/spipesex-02a.expect
@tangler spipesex-03.flx=test/regress/rt/spipesex-03.flx
@tangler spipesex-03.expect=test/regress/rt/spipesex-03.expect
@tangler spipesex-04.flx=test/regress/rt/spipesex-04.flx
@tangler spipesex-04.expect=test/regress/rt/spipesex-04.expect
@tangler spipesex-05.flx=test/regress/rt/spipesex-05.flx
@tangler spipesex-05.expect=test/regress/rt/spipesex-05.expect

@tangler subrout-01.flx=test/regress/rt/subrout-01.flx
@tangler subrout-01.expect=test/regress/rt/subrout-01.expect
@tangler subrout-02.flx=test/regress/rt/subrout-02.flx
@tangler subrout-02.expect=test/regress/rt/subrout-02.expect
@tangler subrout-03.flx=test/regress/rt/subrout-03.flx
@tangler subrout-03.expect=test/regress/rt/subrout-03.expect

@h1 Fibres (fthreads)
@tangle fibres.flx

//$ Low level management of Felix fthreads (fibres).
open class Fibres
{
  private gen _start[t]: (t->0)*t->cont = "$1->clone()->call(0,$2)";

  //$ Function to start a continution with argument type t.
  gen start[t] (p:t->0) (x:t) = { return _start (p,x); }

  private fun _start0: (1->0)->cont = "$1->clone()->call(0)";

  //$ Function to start a contiuation without an argument.
  gen start (p:1->0) = { return _start0 (p); }

  //$ Function to make a fibre out of a continuation.
  gen mk_thread: cont->fthread = "new(*PTF gcp,::flx::rtl::_fthread_ptr_map,false) ::flx::rtl::fthread_t($1)";

  // Spawn a fibre on this fibres scheduler.
  // uses a supervisor call so can't be used in a function
  proc spawn_fthread(p:1->0)
  {
      var con = start p;              // get continuation of p
      var fthr = mk_thread con;
      svc$ svc_spawn_detached fthr;
  }

  proc schedule_fthread(p:1->0)
  {
      var con = start p;              // get continuation of p
      var fthr = mk_thread con;
      svc$ svc_schedule_detached fthr;
  }

  proc swait() { svc$ svc_swait; }

  proc chain : cont = "return $1;";

  //$ The type of a fibre scheduler.
  type fibre_scheduler = "::flx::run::sync_sched*" requires header '#include "flx_sync.hpp"';

  //$ Construct a fibre scheduler.
  //$  NOTE: NOT GARBAGE COLLECTED!
  ctor fibre_scheduler: bool = """new ::flx::run::sync_sched($1, PTF gcp, 
    new ::std::list<::flx::rtl::fthread_t*>)"""
  ;
  ctor fibre_scheduler () =>
    fibre_scheduler (Env::getenv "FLX_DEBUG_DRIVER" != "")
  ;
     

  // NOTE: all this derooting shouldn't be required
  // because normally the deletion balances the construction
  // on a function's machine stack, you can't get up to the
  // deletion until all the active threads are gone
  // because the spawn_fthread runs the fthread immediately
  proc delete_fibre_scheduler : fibre_scheduler = """
    if ($1->ft) $1->collector->remove_root($1->ft);
    for(
     ::std::list<::flx::rtl::fthread_t*>::iterator p = $1->active->begin();
     p != $1->active->end();
     p++
    )
    $1->collector->remove_root(*p);
    delete $1->active; delete $1->ft; delete $1;
  """;

  //$ Spawn a fibre on a given scheduler with a given continuation.
  //$ Note: does NOT run it!
  proc spawn_fibre: fibre_scheduler * fthread = """
    $1->collector->add_root($2);
    $1->active->push_back($2);
  """;

  proc frun: (1->0) = "::flx::rtl::executil::frun (PTF gcp, $1);" 
    requires header '#include "flx_executil.hpp"'
  ;

  proc run: fibre_scheduler = "$1->frun();";

  proc run (p: 1 -> 0) {
    var s = fibre_scheduler();
    spawn_fthread s p;
    s.run;
    delete_fibre_scheduler s;
  }

 
  //$ The type of the stop state of the fibre scheduler.
  //$ terminated: the scheduler is terminated.
  //$ blocked: the scheduler is out of threads to run.
  //$ delegated: the scheduler has been issued a service
  //$  request by a thread which it cannot satisfy.
  //$  The scheduler is put in delegated state and awaits
  //$  for another service to satisfy the request and put
  //$  it back in operation.
  //$
  //$ Note: there is no "operating" state because the
  //$ stop state can only be queried by the schedulers caller
  //$ when the scheduler returns control to it.
  enum fibre_scheduler_state {
    terminated, 
    blocked,   
    delegated  
  };
  fun get_state : fibre_scheduler -> fibre_scheduler_state = "$1->fs";


  //$ Core user procedure for launching a fibre.
  proc spawn_fthread (fs:fibre_scheduler) (p:1->0) { spawn_fibre (fs,p.start.mk_thread); }

  //$ Execute a single step of a fibre.
  gen step: cont -> cont = "$1->resume()";

  //$ Schedule death of a fibre.
  proc kill: fthread = "$1->cc = 0;";

  //$ Run a continuation until it terminates.
  //$ Do not use this proc if the underlying
  //$ procedure attempts to read messages.
  //$ This is a low level primitive, bypassing fthreads.
  proc run: cont = "::flx::rtl::executil::run($1);" requires package "flx_executil";

  private proc _send[t]: &cont * t =
  """
  {
    using namespace ::flx::rtl;
    con_t *tmp = *(con_t**)$1.get_data();
    // run target until it reaches a service request (or death)
    while(tmp && (!tmp->p_svc || tmp->p_svc->variant == svc_yield)) {
      try { tmp=tmp->resume(); }
      catch (con_t *x) { tmp = x; }
    }
    // check it is alive and making the expected service request
    if (!tmp)
      throw flx_exec_failure_t (__FILE__,"send","Send to terminated procedure");
    if (!tmp->p_svc)
      throw flx_exec_failure_t (__FILE__,"send","Send to unready Procedure");
    if (tmp->p_svc->variant != svc_read)
      throw flx_exec_failure_t (__FILE__,"send","Send to Procedure which is not trying to read");
    // store the message
    **(?1**)tmp->p_svc->data= $2;
    // clear the service request
    tmp->p_svc = 0;
    // run the target until the next service request (or death)
    while(tmp && (!tmp->p_svc || tmp->p_svc->variant == svc_yield)) {
      try { tmp=tmp->resume(); }
      catch (con_t *x) { tmp = x; }
    }
    // save the new continuation
    *(con_t**)$1.get_data() = tmp;

  }
  """;

  //$ Send a message to a continuation.
  //$ There is no type checking on the message type.
  //$ The procedure is executed until
  //$ the next wait_state, then the message is stored.
  //$ Low level primitive, bypassing fthreads.
  proc send[t] (p:&cont) (x:t)
  {
    _send (p,x);
  }

}

@h1 Synchronous Channels
@tangle schannels.flx

//$ Sychronous Channels.
//$ Used to exchange control and possibly data
//$ between Felix f-threads (aka fibres).

open class Schannel 
{
  //$ The type of a bidirectional synchronous channel.
  _gc_pointer type schannel[t] = "::flx::rtl::schannel_t*";

  //$ The type of an input synchronous channel.
  _gc_pointer type ischannel[t] = "::flx::rtl::schannel_t*";

  //$ The type of an output synchronous channel.
  _gc_pointer type oschannel[t] = "::flx::rtl::schannel_t*";

  gen mk_untyped_schannel: 1 -> address = 
    "new(*PTF gcp,::flx::rtl::schannel_ptr_map,false) ::flx::rtl::schannel_t(PTF gcp)"
    requires property "needs_gc"
  ;
  //$ Create a bidirectional synchronous channel.
  gen mk_schannel[t]():schannel[t] => 
    C_hack::cast[schannel[t]] #mk_untyped_schannel
  ;

  //$ Model a NULL pointer as an schannel. 
  //$ Necessary for killing off schannels,
  //$ so as to make them unreachable, so the gc can reap them.
  //$ Note: null_schannels are safe.
  gen mk_null_schannel[t]: 1->schannel[t] = "NULL";

  //$ Model a NULL pointer as an ischannel. 
  //$ Necessary for killing off schannels,
  //$ so as to make them unreachable, so the gc can reap them.
  gen mk_null_ischannel[t]: 1->ischannel[t] = "NULL";

  //$ Model a NULL pointer as an oschannel. 
  //$ Necessary for killing off schannels,
  //$ so as to make them unreachable, so the gc can reap them.
  gen mk_null_oschannel[t]: 1->oschannel[t] = "NULL";

  ctor[T] address: oschannel[T] = "$1";
  ctor[T] address: ischannel[T] = "$1";

  //$ Check if an schannel is NULL.
  fun isNULL[T] :schannel[T] -> bool = "NULL==$1";

  //$ Check if an ischannel is NULL.
  fun isNULL[T] :ischannel[T] -> bool = "NULL==$1";

  //$ Check if an oschannel is NULL.
  fun isNULL[T] :oschannel[T] -> bool = "NULL==$1";

  //$ Safe cast from bidirectional to ouput synchronous channel.
  ctor[t] oschannel[t](x:schannel[t]) => C_hack::cast[oschannel[t]] x;

  //$ Safe cast from bidirectional to input synchronous channel.
  ctor[t] ischannel[t](x:schannel[t]) => C_hack::cast[ischannel[t]] x;

  //$ Make an input and an output channel out of a bidirectional channel.
  gen mk_ioschannel_pair[t](var ch:schannel[t]) =>
    ischannel[t] ch, oschannel[t] ch
  ;

  //$ Construct a connected input and output channel pair.
  gen mk_ioschannel_pair[t]() =>
    mk_ioschannel_pair[t]$ mk_schannel[t] ()
  ;

  // pass in address of location to put the pointer to the T data
  proc read[T] (chan:schannel[T], loc: &&T) {
    svc$ svc_sread$ C_hack::cast[_schannel] chan, C_hack::reinterpret[&root::address] (loc);
  }

  // pass in address of location to put the T data
  proc read[T] (chan:schannel[T], p: &T) {
    var loc: &T;
    read (chan, &loc);
    p <- *loc;
  }

  //$ Read an item from a bidirectional channel.
  inline gen read[T] (chan:schannel[T]) = {
    var loc: &T;
    read (chan, &loc);
    return *loc;
  }
  proc read[T] (chan:ischannel[T], loc: &&T) { read (C_hack::cast[schannel[T]] chan, loc); }
  proc read[T] (chan:ischannel[T], p: &T) { read (C_hack::cast[schannel[T]] chan, p); }

  //$ Read an item from an input channel.
  inline gen read[T] (chan:ischannel[T]) => read$ C_hack::cast[schannel[T]] chan;

  //$ Test if channel is read for a read.
  inline gen ready[T] :ischannel[T] -> bool = "$1->waiting_to_write!=NULL";
  inline gen ready[T] : schannel[T] -> bool = "$1->waiting_to_write!=NULL";

  //$ Return Some value if ready, otherwise None
  inline gen maybe_read[T] (chan:ischannel[T]) =>
    if chan.ready then Some chan.read else None[T]
  ;

  inline gen maybe_read[T] (chan:schannel[T]) =>
    if chan.ready then Some chan.read else None[T]
  ;

  //$ Write an item to a bidirectional channel.
  proc write[T] (chan:schannel[T], v:T) {
    var ps = C_hack::cast[root::address]$ new v;
    svc$ svc_swrite$ C_hack::cast[_schannel] chan, &ps;
  }
 
  //$ Multi Write an item to a bidirectional channel.
  proc broadcast[T] (chan:schannel[T], v:T) {
    var ps = C_hack::cast[root::address]$ new v;
    svc$ svc_multi_swrite$ C_hack::cast[_schannel] chan, &ps;
  }
 
  //$ Multi Write an item to an output channel.
  proc write[T] (chan:oschannel[T], v:T) { 
    write (C_hack::cast[schannel[T]] chan, v); 
  }
  proc broadcast[T] (chan:oschannel[T], v:T) { 
    broadcast (C_hack::cast[schannel[T]] chan, v); 
  }

  // Very high power though not very efficient conversion
  // from ischannel to iterator.
  // Given i: ischannel[T] you can just write
  // for j in i do .. done
  gen iterator[T] (i:ischannel[T]) () : opt[T] = {
  next:>
    var y = None[T];
    frun { var x = read i; y = Some x; };
    match y do
    | Some _ => yield y; goto next;
    | None => return y;
    done
  }

  // Here is a subroutine call, assuming the
  // fibre is already created
  inline gen subcall[r,w] (chout:%>w, chin:%<r) (arg:w):r =
  {
    write (chout,arg);
    return read chin;
  }

  // Now, we can use the channels AS a function:
  inline fun apply[r,w] (ch:(%>w * %<r), arg:w):r =>
    subcall ch arg
  ;

}

@h1 Synchronous multiplexor
The following device acts like a select, that is, the reader
get all the input data, but the order is indeterminate.

[Not clear how this is useful .. ]

@tangle mux.flx

//$ Schannel multiplexor.
//$ Read multiple input schannels, write to an output schannel.
open class Multiplexor
{
  //$ Schannel copy.
  noinline proc copy[T] (i:ischannel[T],o:oschannel[T]) () 
  {
    while true do 
      var x = read i;
      write (o,x);
    done
  }

  //$ Schannel multiplexor based on iterator argument.
  //$ Accepts stream of input schannels.
  //$ Writes to output schannel.
  proc mux[T] (inp:1->opt[ischannel[T]], out:oschannel[T]) ()
  {
    for i in inp do 
      spawn_fthread$ copy(i,out); 
    done 
  }


  //$ Schannel multiplexor based on streamable data structure.
  //$ Creates stream of input schannels.
  //$ Writes to output schannel.
  fun mux[C,T with Streamable[C,ischannel[T]]] (a:C, out:oschannel[T]) =>
    mux (iterator a, out)
  ;
}
@h1 Schannel and Pipe syntax
Special syntax for both pipes and also abbreviation for
schannel types.
@tangle spipeexpr.fsyn
syntax spipeexpr 
{
  //$ Left assoc, for schannel pipes.
  x[ssetunion_pri] := x[ssetunion_pri] "|->" x[>ssetunion_pri] =># "(infix 'pipe)"; 

  //$ Right assoc, for schannel pipes transformers
  // => BREAKS PATTERN MATCHING, replaced with >=> but can't find any uses
  //x[ssetunion_pri] := x[>ssetunion_pri] ">=>" x[ssetunion_pri] =># "(infix 'trans_type)"; 

  //$ Non associative, streaming data structure into transducer.
  x[ssetunion_pri] := x[>ssetunion_pri] ">->" x[>ssetunion_pri] =># "(infix 'xpipe)"; 

  //$ input schannel type %<T
  x[sprefixed_pri] := "%<" x[spower_pri] =># '`(ast_name ,_sr "ischannel" (,_2))';

  //$ output schannel type %>T
  x[sprefixed_pri] := "%>" x[spower_pri] =># '`(ast_name ,_sr "oschannel" (,_2))';

  //$ input/output schannel type %<>T
  x[sprefixed_pri] := "%<>" x[spower_pri] =># '`(ast_name ,_sr "ioschannel" (,_2))';

  //$ duplex schannel type %<INPUT%>OUTPUT
  x[sprefixed_pri] := "%<" x[spower_pri] "%>" x[spower_pri] =># 
    '`(ast_name ,_sr "duplex_schannel" (,_2 ,_4))'
  ;


}



@h1 Synchronous Pipeline Concepts
Felix provides synchronous fibres (fthreads) and channels (schannels) 
which can be used to construct active circuits. The simplest case
of these circuits is a pipeline.

Let us open our class and begin.
@tangle spipes.flx
//$ Synchronous Pipes.
open class Spipe {

@h1 Sources and Sinks.
@h2 An infinite Source.
A source is an thread which writes output down a channel.
We will wrap a generator up to create a source.
@tangle spipes.flx
  //$ Send an stream down a channel.
  proc source_from_stream[S] (var it:1 -> S) (out:%>S) =>
    while true call write (out,#it)
  ;

@h2 A finite Source.
Here, we use an iterator returning an stream of optional values
to create a finite source. If there is @{Some value} we return
the @{value} otherwise we just terminate.
@tangle spipes.flx
  //$ source from an iterator
  proc source_from_iterator[S] (var it:1 -> opt[S]) (out:%>S) =>
    match v in it call write$ out,v
  ;

@h2 An infinite Sink.
A sink is a procedure which reads from a channel.
We will wrap up an ordinary procedure into a sink.
@tangle spipes.flx
  proc sink_from_procedure[T] (var p:T -> 0) (inp:%<T) => 
    while true call p (read inp)
  ;

@h2 A basic pipeline
Now we need to connect our source and sink.
The @{pipe} operator below takes a pair
of arguments, the sink procedure comes first,
then the source. The function has an infix ascii art
operator which can be used conveniently: @{|->}.

@tangle spipes.flx
  //$ Wire a source component to a sink.
  //$ Return coupled fibre ready to run.
  inline fun pipe[T] 
    (var w: %>T -> 0, // source
    var r: %<T -> 0)  // sink
  :
    1 -> 0
  => 
    {
      var chi,cho = mk_ioschannel_pair[T] ();
      spawn_fthread { (w cho); };
      spawn_fthread { (r chi); };
    }
  ;
@
It is important to note this function does nothing
but return a procedure closure, it doesn't actually
start anything running. It creates a single synchronous
channel with an input and output endpoint shared by
the source and sink. The closure is essential to hide
these variables to ensure the source and sink
suicide together when they become unreachable.

@h2 A simple example
Now we will show a simple example.
@tangle spipesex-01.flx

begin
  var src = (1,2,3,4,5).iterator.source_from_iterator;
  var snk = println[int].sink_from_procedure;

  #(src |-> snk);
end
@

@tangle spipesex-01.expect
1
2
3
4
5
@

@h1 Transducers.
A transducer is a procedure which reads from one channel
and writes to another. The most basic transducers can be
created from a function. 
@tangle spipes.flx
  proc transducer_from_function[O,I] (f:I->O) 
    (var r: %<I,
    var w: %>O)
  =>
    while true call write$ w, r.read.f;
  ;
@
@h2 Wire transducer to sink.
In order to use a transducer we have to be able to wire it
to other components. First, wire one into a sink, producing
another sink.

@tangle spipes.flx
  //$ Wire a transducer into a sink.
  //$ Return a sink.
  inline fun pipe[T,U]
    (var a: %<T * %>U -> 0,
    var b: %<U -> 0)
  :
    %<T  -> 0 
  => 
    proc (inp:%<T)
    {
      var chi,cho = mk_ioschannel_pair[U] ();
      spawn_fthread { a (inp, cho); };
      spawn_fthread { b (chi); };
    }
  ;
@
@h2 Wire transducer to source.
Alternately we can wire a transducer into a source, producing
another source.

@tangle spipes.flx
  //$ Wire a source component to a transducer.
  //$ Return source.
  inline fun pipe[T,U]
    (var w: %>T -> 0,
    var t: %<T * %>U -> 0)
  :
    %>U -> 0 
  => 
    proc (out:%>U)
    {
      var chi,cho = mk_ioschannel_pair[T] ();
      spawn_fthread { (w cho); };
      spawn_fthread { (t (chi, out)); };
    }
  ;
@
@h2 Wire transducer to transducer.
Also we would like to be able to wire two transducers together,
producing another transducer.
@tangle spipes.flx
  //$ Wire a transducer into a transducer.
  //$ Return another transducer.
  inline fun pipe[T,U,V]
    (var a: %<T * %>U -> 0,
    var b: %<U * %>V -> 0)
  :
    %<T * %>V -> 0 
  => 
    proc (inp:%<T, out:%>V)
    {
      var chi,cho = mk_ioschannel_pair[U] ();
      spawn_fthread { a (inp, cho); };
      spawn_fthread { b (chi, out); };
    }
  ;
@

@h2 A simple example
Here is an example:

@tangle spipesex-02.flx
begin
  var src = (1,2,3,4,5).iterator.source_from_iterator;
  var snk = println[int].sink_from_procedure;
  var inc = transducer_from_function (fun (x:int)=> x + 1);
  var sqr = transducer_from_function (fun (x:int) => x * x);
  println$ "--";
  #(src |-> inc |-> sqr |-> snk);
  println$ "--";
  src = (1,2,3,4,5).iterator.source_from_iterator;
  #((src |-> inc) |-> sqr |-> snk);
  println$ "--";
  src = (1,2,3,4,5).iterator.source_from_iterator;
  #(src |-> (inc |-> sqr) |-> snk);
  println$ "--";
  src = (1,2,3,4,5).iterator.source_from_iterator;
  #(src |-> inc |-> (sqr |-> snk));
  println$ "--";
end
@
Note the vital feature: the pipe operator is associative!
@tangle spipesex-02.expect
--
4
9
16
25
36
--
4
9
16
25
36
--
4
9
16
25
36
--
4
9
16
25
36
--
@
@h1 A more direct overload.
We can make the pipe operator provide a more direct overloads.
Note carefully the danger of these impure operators:
specialisation rules will generally select the right one,
unless your generator, function, or procedure happens to
want to send schannels themselves down schannels!

@tangle spipes.flx
  // source to sink: autobuild both arguments
  inline fun pipe[T] (w:1->T,r:T->0) => 
     w.source_from_stream |-> r.sink_from_procedure;

  inline fun pipe[T] (w:1->opt[T],r:T->0) => 
     w.source_from_iterator |-> r.sink_from_procedure;

  // source to sink: autobuild only one argument
  inline fun pipe[T] (w:1->T,r:%<T->0) => 
     w.source_from_stream |-> r;

  inline fun pipe[T] (w:1->opt[T],r:%<T->0) => 
     w.source_from_iterator |-> r;

  inline fun pipe[T] (w:%>T->0,r:T->0) => 
     w |-> r.sink_from_procedure;

  // source to transducer: autobuild both arguments
  inline fun pipe[T0,T1] (w:1->T0,f:T0->T1) => 
     w.source_from_stream |-> f.transducer_from_function;

  inline fun pipe[T0,T1] (w:1->opt[T0],f:T0->T1) => 
     w.source_from_iterator |-> f.transducer_from_function;

  // source to transducer: autobuild one argument
  inline fun pipe[T0,T1] (w:%>T0->0,f:T0->T1) => 
     w |-> f.transducer_from_function;

  inline fun pipe[T0,T1] (w:1->T0,f:%<T0*%>T1->0) => 
     w.source_from_stream |-> f;

  inline fun pipe[T0,T1] (w:1->opt[T0],f:%<T0 *%>T1->0) => 
     w.source_from_iterator |-> f;

  // transducer to transducer: autobuild both arguments
  inline fun pipe[T0,T1,T2] (f1:T0->T1,f2:T1->T2) => 
    f1.transducer_from_function |-> f2.transducer_from_function;

  // transducer to transducer: autobuild one argument
  inline fun pipe[T0,T1,T2] (f1:T0->T1,f2:%<T1 * %>T2 ->0) => 
    f1.transducer_from_function |-> f2;

  inline fun pipe[T0,T1,T2] (f1:%<T0 * %>T1->0,f2:T1->T2) => 
    f1 |-> f2.transducer_from_function;

  // transducer to sink: autobuild both arguments
  inline fun pipe[T0,T1] (f:T0->T1, r:T1->0) =>
    f.transducer_from_function |-> r.sink_from_procedure;

  // transducer to sink: autobuild one argument
  inline fun pipe[T0,T1] (f:T0->T1, r:%<T1->0) =>
    f.transducer_from_function |-> r;

  inline fun pipe[T0,T1] (f:%<T0 * %>T1->0, r:T1->0) =>
    f |-> r.sink_from_procedure;

@

@h2 A simple example
Here is an example:

@tangle spipesex-02a.flx
begin
  #((1,2,3,4,5).iterator |-> (fun (x:int) => x + 1) |-> (fun (x:int) => x * x) |-> println[int]);
end
@
Note the vital feature: the pipe operator is associative!
@tangle spipesex-02a.expect
4
9
16
25
36
@

@h1 Raw Devices.
So far we have introduced a simple method for creating three kinds of device,
with a simple form of operation, and a method for connecting them in a pipeline.

You may wonder what happens if a device in a pipeline simply returns.
The answer is simple. Nothing happens. The pipeline eventually collapses.
Control resumes after the statement constructing the pipeline.

In fact we have already seen this behaviour when we connected a
finite source at the head of a pipeline.

Let us look now at a more complex system. To do this we need to
construct raw devices. We will make a simple tokeniser.
First our source emits characters:

@tangle spipesex-03.flx
proc source(w:%>char)
{
  var s = "The quick brown fox jumped over the lazy dog";
  for ch in s call write (w,ch);
  write (w,char 0);
}
@
Notice this source simply returns when there is no data left,
after emitting a terminating nul character.

Now we will do the sink which prints the tokens:
@tangle spipesex-03.flx
proc sink (r:%<string)
{
  while true call println$ read r;
}
@
This is an infinite loop, but it will stall when it can't
read any more tokens. Obviously we want the output to be:
@tangle spipesex-03.expect
The
quick
brown
fox
jumped
over
the
lazy
dog
@

Finally we will write the tokeniser:
@tangle spipesex-03.flx
proc tokenise (i:%<char, o:%>string)
{
  var s = "";
skipwhite:>
  var ch = read i;
  if ch == ' ' goto skipwhite;
gather:>
  if ch == char 0  goto fin;
  s += string ch;
  ch = read i;
  if ch != ' ' goto gather;
  write (o,s);
  s = "";
  if ch != char 0 goto skipwhite;
fin:>
  if s != "" call write (o,s);
}
@

What is important to observe here is that there are multiple reads on the
input, for each output. Finally the pipeline:

@tangle spipesex-03.flx
#(source |-> tokenise |-> sink);
@

@h1 Bi-connectors
We will now provide some ways to make slightly more
complex circuits. Bi-connectors work with pairs
of channels. 

@h2 Wire two sources to two sinks.
The most basic pipeline.

@tangle spipes.flx
 
  //$ Wire two sources to two sinks.
  //$ Return a bi-pipeline!
  fun pipe[T0,T1]
    (
      var w: 
        (%>T0 -> 0) * 
        (%>T1 -> 0),
      var r:
        (%<T0 -> 0) * 
        (%<T1 -> 0)
    )
  :
    1 -> 0
  =>
    {
       pipe (w.0,r.0) ();
       pipe (w.1, r.1) ();
    }
  ;


@h2 Wire two sources to two transducers.
Construct a pair of sources from a pair of sources and
a pair of transducers.

@tangle spipes.flx
  //$ Wire two source components to two transducers.
  //$ Return two sources.
  inline fun pipe[T0,T1,U0,U1]
    (
      var w: 
        (%>T0 -> 0) *
        (%>T1 -> 0),
      var t: 
        (%<T0 * %>U0 -> 0) *
        (%<T1 * %>U1 -> 0)
    )
  :
    (%>U0 -> 0) *
    (%>U1 -> 0)
  => 
    pipe (w.0,t.0),
    pipe (w.1,t.1)
  ;

@h2 Wire two transducers to two sinks.
Returns a pair of sinks.

@tangle spipes.flx
  //$ Wire two transducers into two sinks.
  //$ Return two sinks.
  inline fun pipe[T0,T1,U0,U1]
    (
      var a: 
        (%<T0 * %>U0 -> 0) *
        (%<T1 * %>U1 -> 0),
      var b: 
         (%<U0 -> 0) *
         (%<U1 -> 0)
    )
  :
    (%<T0  -> 0)  *
    (%<T1  -> 0) 
  => 
      pipe (a.0, b.0),
      pipe (a.1, b.1)
  ;


@h2 Wire two transducers to two transducers.
Returns a pair of transducers.

@tangle spipes.flx

  //$ Wire two transducers into two transducers.
  //$ Return two transducers.
  inline fun pipe[T0,T1,U0,U1,V0,V1]
    (
      var a: 
        (%<T0 * %>U0 -> 0) *
        (%<T1 * %>U1 -> 0),
      var b: 
        (%<U0 * %>V0 -> 0) * 
        (%<U1 * %>V1 -> 0) 
    )
  :
    (%<T0 * %>V0 -> 0) *
    (%<T1 * %>V1 -> 0)
  => 
     pipe (a.0, b.0),
     pipe (a.1, b.1)
  ;
@

@h2 A simple example.
@tangle spipesex-04.flx

// sources
var src1 = (1,2,3,4,5).iterator.source_from_iterator;
var src2 = (1,2,3,4,5).iterator.source_from_iterator;

// transducers
var inc = transducer_from_function (fun (x:int)=> x + 1);
var sqr = transducer_from_function (fun (x:int) => x * x);
var str1 = Str::str[int].transducer_from_function;

// sinks
// we will save the results and print them because
// the order of accepting the data between the
// two sinks is indeterminate
var result1 = list[string]();
var result2 = list[string]();
proc set1(s:string) => result1 = s ! result1;
proc set2(s:string) => result2 = s ! result2;
var snk1 = set1.sink_from_procedure;
var snk2 = set2.sink_from_procedure;

// pipeline
#((src1,src2)  |-> (inc,sqr) |-> (str1,str1) |-> (snk1,snk2));

// show results
println$ result1;
println$ result2;
@

@tangle spipesex-04.expect
list('6', '5', '4', '3', '2')
list('25', '16', '9', '4', '1')
@

@h1 Multi-connectors
@tangle spipes.flx
 
  //$ Utility for tee connector: array of outputs
  proc run_tee[T,N] (i:%<T, oa:(%>T)^N)
  {
  again:>
    var d = read i;
    for o in oa do
      write (o, d);
    done
    goto again;
  }


  //$ Wire together a source component with an array of sinks.
  //$ Returns a closed pipeline.
  inline fun pipe[T,N] 
  (
    var w: %>T -> 0,
    var r: (%<T -> 0) ^ N
  )
    : 1 -> 0
  =>
    {
      var i,o = #mk_ioschannel_pair[T];
      spawn_fthread { w o; };
      var cho : (%>T) ^N;
      for var j in 0uz upto r.len - 1uz do
        var inp,out = #mk_ioschannel_pair[T];
        &cho.j <- out;
        noinline proc exec (var k:size, chi:%<T) () { r.k chi; }
        spawn_fthread$ exec (j,inp);
      done
      spawn_fthread { run_tee (i,cho); };
    }
  ;


  //$ Wire together a source component with two transducers
  //$ Returns two sources.
  //
  // WARNING: I do not know why this one works.
  // It actually starts the threads when called.
  // This is wrong, but I see no alternative.
  //
  // Also, it's a function starting those threads!
  // Functions cannot run threads at the moment.
  // Unless they manually construct a scheduler.

  inline fun pipe[T,U0,U1] 
  (
    var w: %>T -> 0,
    var r: (%<T * %>U0 -> 0) * (%<T * %>U1 -> 0)
  )
  :
    (%>U0 -> 0) * (%>U1 -> 0)
  =
  {
      var i,o = #mk_ioschannel_pair[T];
      var chi0,cho0 = #mk_ioschannel_pair[T];
      var chi1,cho1 = #mk_ioschannel_pair[T];
      spawn_fthread { run_tee (i,(cho0, cho1)); };
      spawn_fthread { w o; };
      return
        proc (out:%>U0) { r.0 (chi0,out); },
        proc (out:%>U1) { r.1 (chi1,out); }
      ;
  }



  //$ Stream sort using intermediate darray.
  //$ Requires stream of option type.
  proc sort[T with Tord[T]] (r: %<opt[T], w: %>opt[T])
  {
     var x = darray[T]();
     acquire:while true do
       match read r with
       | Some v => x+=v;
       | #None => break acquire;
       endmatch;
     done
     x.sort;
     for v in x do
       write (w, Some v);
     done
     write (w,None[T]);
  }

} // end class

@

@tangle spipesex-05.flx

// Pipe test.
proc source (cho:%>int) { 
  for var i in 0 upto 9 do write (cho,i); done 
}

proc sink (chi:%<int) { 
  while true do var x= read chi; println x; done 
}

proc xduce(chi: %<int, cho: %>int) {
  write (cho, 99);
  while true do var x = read chi; write (cho, x); write (cho, 2 * x); done
}

spawn_fthread$  source |-> xduce |-> sink;
@

@tangle spipesex-05.expect
99
0
0
1
2
2
4
3
6
4
8
5
10
6
12
7
14
8
16
9
18
@

@h1 Subroutines
In this section, we will provides some special uses of fibres and 
schannels.

The usual protocol for calling a function is to call the function passing
an argument and wait to receive back a value. We can emulate this with a 
transducer already.

@tangle subrout-01.flx
proc int_to_string (chi: %<int, cho: %>string)  ()
{
  var x = read chi;
  var r = x.str;
  write(cho, r);
}
var ain, aout = mk_ioschannel_pair[int]();
var rin, rout = mk_ioschannel_pair[string]();
spawn_fthread$ int_to_string (ain, rout);
write (aout, 42);
var r = read rin;
println$ r;
@
@tangle subrout-01.expect
42
@

Most notable in this structure is that the write and read from
the mainline can be split. We expect that in the subroutine,
the argument is passed at the beginning and the result returned
at the end. In a non-looping subroutine, we can do some initialisation
work before the read. However calculations performed after the write
are useless because the fibre suicides.

The split in the mainline, however, is unusual. If the subroutine
were to wait on an asynchronous event, the waiting time could
be used by the mainline until it requires the result. Otherwise
there is no good reason not to demand the result immediately.

This suggests the mainline should write and read in a single
operation. Similarly the subroutine should terminate with the
final write.

In addition, we note that we have used two schannels here with
two ends each. But clearly a single channel is enough because
of the subroutine call protocol.

This therefore demands a new schannel typing arrangement.
@tangle schannels.flx

open class DuplexSchannels
{
_gc_pointer type duplex_schannel[r,w] = "::flx::rtl::schannel_t*";

inline gen read[r,w] (chan:duplex_schannel[r,w]) : r =>
  read (C_hack::cast[ischannel[r]] chan)
;

inline proc write[r,w] (chan:duplex_schannel[r,w], v:w)  =>
  write (C_hack::cast[oschannel[w]] chan, v)
;

ctor[r,w] duplex_schannel[r,w] () =>
  C_hack::cast[duplex_schannel[r,w]] #mk_untyped_schannel
; 

// NOTE: assuming the mainline want to read an r
// after passing a w to the subroutine, it must
// use the second channel of the pair to do so.
// passing the first one to the subroutine.
gen mk_duplex_schannel_pair[r,w] () =>
  let c = #mk_untyped_schannel in
  C_hack::cast[duplex_schannel[w,r]] c,
  C_hack::cast[duplex_schannel[r,w]] c
;

// Here is our subroutine call, assuming the
// fibre is already created
inline gen subcall[r,w] (ch:duplex_schannel[r,w]) (arg:w):r =
{
  write (ch,arg);
  return read ch;
}

// Now, we can use the duplex channel AS a function:
inline fun apply[r,w] (ch:duplex_schannel[r,w], arg:w):r =>
  subcall ch arg
;

// Here is a self contained subcall that spawns the fibre
// and creates the channel too. This model is for a one shot.
inline gen subcall[r,w] 
  (fib: duplex_schannel[w,r] -> 1 -> 0)
  (arg: w)
: r =
{
  var wr,rw = mk_duplex_schannel_pair[r,w]();
  spawn_fthread$ fib wr;
  write (rw,arg);
  return read rw;
}

inline gen apply[r,w] (
  fib: duplex_schannel[w,r] -> 1 -> 0,
  arg: w)
: r =>
  subcall fib arg
;

} // class DuplexSchannels

@
Let's now rewrite our example:
@tangle subrout-02.flx
proc int_to_string (ch: %<int%>string)  ()
{
  var x = read ch;
  var r = x.str;
  write(ch, r);
}
var wr, rw = mk_duplex_schannel_pair[string,int]();
spawn_fthread$ int_to_string wr;
println$ rw 42;
@
@tangle subrout-02.expect
42
@

Even more compactly:

@tangle subrout-03.flx
proc int_to_string (ch: %<int%>string)  ()
{
  var x = read ch;
  var r = x.str;
  write(ch, r);
}
println$ int_to_string 42;
@
@tangle subrout-03.expect
42
@


