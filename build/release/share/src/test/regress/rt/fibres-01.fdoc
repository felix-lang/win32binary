
@h1 fibres-01.flx
@felix
include "std/control/schannels";
include "std/io/faio";

var sys_clock =  #Faio::mk_alarm_clock;

noinline proc mk_xfib(k:int, inp:ischannel[int],out:oschannel[int])
{
  spawn_fthread {
    for var count in 1 upto 3 do
      var i = read(inp);
      write (out,i+1);
      Faio::sleep(sys_clock,0.1);
    done
  };
}

noinline proc mkchain() {
  def var in0, var out0 = #mk_ioschannel_pair[int];
  var in1: ischannel[int];
  var out1: oschannel[int];
  for var k in 1 upto 5 do
    in1, out1 = #mk_ioschannel_pair[int];
    mk_xfib(k,in0, out1);
    in0 = in1;
  done
  spawn_fthread {
    write(out0, 11);
    write(out0, 21);
    write(out0, 31);
  };
  var m1 = in0.read;
  var m2 = in0.read;
  var m3 = in0.read;
  println$ "Read1="+ m1.str;
  println$ "Read2="+ m2.str;
  println$ "Read3="+ m3.str;
}

mkchain();

println "mainline main fibre done";


@expect
Read1=16
Read2=26
Read3=36
mainline main fibre done
@
